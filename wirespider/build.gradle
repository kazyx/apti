buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'findbugs'
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group = 'net.kazyx'
    version = '1.2.2'

    repositories {
        jcenter()
    }

    def utf8 = 'UTF-8'
    [compileJava, compileTestJava]*.options*.encoding = utf8

    sourceCompatibility = 7
    targetCompatibility = 7

    findbugs {
        ignoreFailures = true
        sourceSets = [sourceSets.main]
        effort = "max"
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc {
        options.locale = 'en_US'
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenPublication(MavenPublication) {
                from components.java
            }
        }
    }

    bintray {
        user = project.hasProperty('bintray_user') ? bintray_user : ''
        key = project.hasProperty('bintray_apikey') ? bintray_apikey : ''

        pkg {
            repo = 'maven'
            // name is set by each subproject
            licenses = ['MIT']
            websiteUrl = 'https://github.com/kazyx/wirespider'
            issueTrackerUrl = 'https://github.com/kazyx/wirespider/issues'
            vcsUrl = 'https://github.com/kazyx/wirespider.git'
            version {
                name = project.version
                released = new Date()
                gpg {
                    sign = true
                    passphrase = project.hasProperty('bintray_gpg_password') ? bintray_gpg_password : ''
                }
            }
        }
        publications = ['mavenPublication']
        configurations = ['archives']

        dryRun = true
    }
}
